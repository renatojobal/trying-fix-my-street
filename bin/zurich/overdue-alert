#!/usr/bin/env perl

# zurich/overdue-alert:
# Send email alerts to administrators for overdue admin activities.
#
# Copyright (c) 2012 UK Citizens Online Democracy. All rights reserved.
# Email: matthew@mysociety.org. WWW: http://www.mysociety.org

use strict;
use warnings;
require 5.8.0;

BEGIN {
    use File::Basename qw(dirname);
    use File::Spec;
    my $d = dirname(File::Spec->rel2abs($0));
    require "$d/../../setenv.pl";
}

use DateTime;
use CronFns;
use FixMyStreet::Cobrand;
use FixMyStreet::DB;
use FixMyStreet::Email;

my ($verbose, $nomail) = CronFns::options();

# Only run on working days
my $now = DateTime->now();
exit if FixMyStreet::Cobrand::Zurich::is_public_holiday($now) or FixMyStreet::Cobrand::Zurich::is_weekend($now);

my $cobrand = FixMyStreet::Cobrand->get_class_for_moniker('zurich')->new();
my %bodies = map { $_->id => $_ } FixMyStreet::DB->resultset("Body")->all;

loop_through( 'alert-moderation-overdue.txt', 0, 1, [ 'submitted' ] );
loop_through( 'alert-overdue.txt', 1, 6, 'in progress' );
loop_through( 'alert-overdue.txt', 0, 6, ['confirmed', 'feedback pending'] );

sub loop_through {
    my ( $template, $include_parent, $days, $states ) = @_;
    my $dtf = FixMyStreet::DB->schema->storage->datetime_parser;
    my $date_threshold = $dtf->format_datetime(FixMyStreet::Cobrand::Zurich::sub_days( $now, $days ));

    my $reports = FixMyStreet::DB->resultset("Problem")->search( {
        state => $states,
        created => { '<',  $date_threshold },
        bodies_str => { '!=', undef },
    } );

    my %to_send = ();
    while (my $row = $reports->next) {
        $to_send{$row->bodies_str} ||= [];
        push @{$to_send{$row->bodies_str}}, $row;
    }

    foreach my $body_id (keys %to_send) {
        send_alert( $template, $body_id, $to_send{$body_id}, $include_parent );
    }
}

sub send_alert {
    my ( $template, $body_id, $data, $include_parent ) = @_;

    my $body = $bodies{$body_id};
    my $body_email = $body->endpoint;

    my $h = {
        data => $data,
        admin_url => $cobrand->admin_base_url,
        cobrand => $cobrand,
    };

    my $to = [ [ $body_email, $body->name ] ];
    if ( $include_parent ) {
        my $parent = $body->parent;
        my $parent_email = $parent->endpoint;
        push @$to, [ $parent_email, $parent->name ];
    }

    FixMyStreet::Email::send_cron(
        FixMyStreet::DB->schema,
        $template,
        $h,
        {
            To => $to,
            From => [ FixMyStreet->config('CONTACT_EMAIL'), FixMyStreet->config('CONTACT_NAME') ],
        },
        FixMyStreet->config('CONTACT_EMAIL'),
        $nomail,
        $cobrand, 'de-ch',
    );
}

